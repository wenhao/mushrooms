plugins {
    id 'java-library'
    id 'idea'
//    id 'java'
    id 'checkstyle'
    id "org.springframework.boot" version '2.0.4.RELEASE'
    id "com.gorylenko.gradle-git-properties" version "2.0.0"
    id "org.owasp.dependencycheck" version "5.0.0-M2"
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'jacoco'
    id "com.github.ksoichiro.spelling" version "0.1.1"
    id "com.simonharrer.modernizer" version "1.6.0-1"
    id 'com.github.ksoichiro.console.reporter' version '0.6.2'
    id 'com.github.ksoichiro.build.info' version '0.2.0'
    id "com.jfrog.bintray" version "1.8.4"
    id 'maven-publish'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.github.wenhao'
version = '3.0.8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Finchley.SR1'
    }
}

dependencies {
    compileOnly "io.github.openfeign:feign-okhttp"
    implementation "org.apache.commons:commons-lang3:3.8.1"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.json:json:20180130'
    implementation 'org.skyscreamer:jsonassert:1.5.0'
    implementation 'com.jayway.jsonpath:json-path:2.4.0'

    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'

    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    testImplementation 'org.springframework.boot:spring-boot-starter-freemarker'
    testImplementation "io.github.openfeign:feign-okhttp"
    testImplementation "com.google.guava:guava:27.1-jre"
    testImplementation 'org.projectlombok:lombok:1.18.0'
    testImplementation 'com.squareup.okhttp3:okhttp-urlconnection:3.8.1'
    testImplementation 'org.apache.commons:commons-pool2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.assertj:assertj-core:3.10.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.2.0"
    testImplementation "commons-io:commons-io:2.6"
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.6'
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.2.0"
}

jar {
    enabled = true
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

compileJava {
    options.incremental = true
}

compileJava.dependsOn(processResources)

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

checkstyle {
    toolVersion '8.1'
    ignoreFailures = false
    configFile file("$project.rootDir/config/checkstyle/checkstyle.xml")
    sourceSets = [sourceSets.main]

    checkstyleMain {
        source = 'src/main/java'
    }
}

test {
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    useJUnitPlatform()
}

copy {
    from "./config/githooks/"
    into "./.git/hooks/"
    fileMode 0755
}

bootJar {
    enabled = false
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(
                    dir: it,
                    exclude: [
                            '**/Application.class',
                            '**/domain/*.class',
                            '**/properties/*.class',
                            '**/repository/*.class',
                            '**/interceptor/*.class',
                            '**/response/*.class',
                            '**/config/*.class',
                            '**/dataloader/*.class'
                    ]
            )
        })
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
                    '**.Application',
                    '**.domain.*',
                    '**.properties.*',
                    '**.repository.*',
                    '**.interceptor.*',
                    '**.response.*',
                    '**.config.*',
                    '**.dataloader.*'
            ]

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
        }
    }
    dependsOn(jacocoTestReport)
}
check.dependsOn jacocoTestCoverageVerification

def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    scm {
        url 'https://github.com/wenhao/mushrooms'
        connection 'https://github.com/wenhao/mushrooms.git'
        developerConnection 'git@github.com:wenhao/mushrooms.git'
    }

    developers {
        developer {
            id 'wenhao'
            name 'Hao Wen'
            email 'wenhao@126.com'
            organization 'Hao Wen'
            roles {
                role 'Developer'
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.github.wenhao'
            artifactId 'mushrooms'
            version '3.0.8'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Mushrooms is an easy setup failover and stub framework. To ensure high levels of efficiency for remote service integration.')
                root.appendNode('name', 'mushrooms')
                root.appendNode('url', 'https://github.com/wenhao/mushrooms')
                root.appendNode('inceptionYear', '2018')
                root.children().last() + pomConfig
            }
        }
    }
}

// publish to jcenter
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publications = ['maven']
    dryRun = false
    publish = true
    override = true
    pkg {
        repo = 'maven'
        name = 'mushrooms'
        desc = 'Mushrooms is an easy setup failover and stub framework. To ensure high levels of efficiency for remote service integration.'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/wenhao/mushrooms'
        issueTrackerUrl = 'https://github.com/wenhao/mushrooms/issues'
        vcsUrl = 'https://github.com/wenhao/mushrooms'
        labels = ['mock', 'stub']
        publicDownloadNumbers = true
        githubRepo = 'wenhao/mushrooms'
        githubReleaseNotesFile = 'README.md'
        version {
            name = '3.0.8'
            desc = 'Mushrooms is an easy setup failover and stub framework. To ensure high levels of efficiency for remote service integration.'
            released = new Date()
            vcsTag = '3.0.8'
            gpg {
                sign = true
                passphrase = 'passphrase'
            }
            mavenCentralSync {
                sync = false
                user = project.hasProperty('sonatypeUsername')
                password = project.hasProperty('sonatypePassword')
                close = '1'
            }
        }
    }
}

